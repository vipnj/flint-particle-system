<?xml version="1.0" encoding="utf-8"?>
<s:Application
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:f="http://flintparticles.org/2009/flint2d"
	width="500" height="350"
	backgroundColor="#000000" creationComplete="complete()">
	
<fx:Script>
	<![CDATA[
		import org.flintparticles.common.events.EmitterEvent;
		import org.flintparticles.common.particles.Particle;
		import org.flintparticles.twoD.actions.Explosion;
		import org.flintparticles.twoD.particles.Particle2DUtils;
		
		private var explosion:Explosion;
		private var bitmap:BitmapData;

		private function complete():void
		{
			bitmap = new Image1( 384, 255 );
			prepare();
			addEventListener( MouseEvent.CLICK, explode, false, 0, true );
			emitter.addEventListener( EmitterEvent.EMITTER_EMPTY, prepare );
		}
		
		private function prepare( event:EmitterEvent = null ):void
		{
			if( explosion )
			{
				emitter.removeAction( explosion );
				explosion = null;
			}
			var particles:Vector.<Particle> = Particle2DUtils.createRectangleParticlesFromBitmapData( bitmap, 8, emitter.particleFactory, 56, 47 );
			emitter.addParticles( particles, false );
		}

		private function explode( ev:MouseEvent ):void
		{
			if( !explosion )
			{
				var p:Point = renderer.globalToLocal( new Point( ev.stageX, ev.stageY ) );
				explosion = new Explosion( 8, p.x, p.y, 500 );
				emitter.addAction( explosion );
			}
		}
	]]>
</fx:Script>

<f:DisplayObjectRenderer id="renderer" width="500" height="350">
	<f:emitters>
		<f:Emitter id="emitter" autoStart="true">
			<f:actions>
				<f:Move/>
				<f:DeathZone zoneIsSafe="true">
					<f:RectangleZone left="-5" right="505" top="-5" bottom="355"/>
				</f:DeathZone>
			</f:actions>
		</f:Emitter>
	</f:emitters>
</f:DisplayObjectRenderer>
</s:Application>
